# Configuracion basada en https://github.com/thoughtbot/dotfiles
# Pero adaptada a adJ

# carga funciones ejecutables
for f in ~/.zsh/funciones/*; do
  source $f
done

# archivo extra en ~/.zsh/conf/pre , ~/.zsh/conf, and ~/.zsh/conf/post
# se cargan primero, segundo y tercero resepctivamente
_load_settings() {
  _dir="$1"
  if [ -d "$_dir" ]; then
    if [ -d "$_dir/pre" ]; then
      for config in "$_dir"/pre/**/*~*.zwc(N-.); do
        . $config
      done
    fi

    for config in "$_dir"/**/*(N-.); do
      case "$config" in
        "$_dir"/(pre|post)/*|*.zwc)
          :
          ;;
        *)
          . $config
          ;;
      esac
    done

    if [ -d "$_dir/post" ]; then
      for config in "$_dir"/post/**/*~*.zwc(N-.); do
        . $config
      done
    fi
  fi
}
_load_settings "$HOME/.zsh/conf"

# Configuracion local
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local

# alias
[[ -f ~/.alias ]] && source ~/.alias
